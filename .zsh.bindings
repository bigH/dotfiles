#!/usr/bin/env zsh

# Ctrl-P - select file to paste
bindkey '^P' fzf-file-widget
# Ctrl-N - select directory to paste
bindkey '^N' fzf-cd-widget

# Alt-O - open files differing from particular commit
fzf-gfi-gh-widget() { local result=$(gfi $(gh) | join-lines); zle reset-prompt; LBUFFER+=$result }
zle -N fzf-gfi-gh-widget
bindkey '^[o' fzf-gfi-gh-widget

# Ctrl-O - open files differing from merge-base
fzf-gfi-widget() { local result=$(gfi | join-lines); zle reset-prompt; LBUFFER+=$result }
zle -N fzf-gfi-widget
bindkey '^O' fzf-gfi-widget

# Ctrl-B/F - back / forward by word
bindkey '^b' backward-word
bindkey '^f' forward-word

# Alt-B/F - back / forward by word
bindkey '^[b' emacs-backward-word
bindkey '^[f' emacs-forward-word

# Alt-Space - insert `git branch-name`
zmodload -i zsh/parameter
insert-git-branch-name() {
  LBUFFER+=`git branch-name`
}
zle -N insert-git-branch-name
bindkey '^[ ' insert-git-branch-name

# Alt-X - insert last command result
zmodload -i zsh/parameter
insert-last-command-output() {
  LBUFFER+="$(eval $history[$((HISTCMD-1))])"
}
zle -N insert-last-command-output
bindkey '^[x' insert-last-command-output

# Ctrl-V - edit the command line in vim
bindkey '^v' edit-command-line

fixup-rg-command-for-cmd-substitution() {
  if [ -z "$1" ]; then
    echo "$1"
  elif [[ "$1" == *'rg'* ]]; then
    # add `-l` to `rg` command if simple and not present
    if [[ "$1" == *' -l '* ]]; then
      echo "$1"
    elif [[ "$1" == *' --files-without-match '* ]]; then
      echo "$1"
    elif [[ "$1" == *'$(rg'* ]]; then
      # too complex to fix-up
      echo "$1"
    else
      echo "$1" | sed 's/rg/rg -l/'
    fi
  fi
}

# Alt-V - `vim $(...)` around
insert-last-command-with-vim() {
  BUFFER='vim $('
  BUFFER+="$(fixup-rg-command-for-cmd-substitution "$history[$((HISTCMD-1))]")"
  BUFFER+=')'
  CURSOR=$(echo "$#BUFFER - 1" | bc)
}
zle -N insert-last-command-with-vim
bindkey '^[v' insert-last-command-with-vim

# Alt-R - `rg $(... -l)` around
insert-last-command-with-rg() {
  BUFFER='rg  -- $('
  BUFFER+="$(fixup-rg-command-for-cmd-substitution "$history[$((HISTCMD-1))]")"
  BUFFER+=')'
  CURSOR=3
}
zle -N insert-last-command-with-rg
bindkey '^[r' insert-last-command-with-rg

# TODO Ctrl-? - select from to run using $(..)
